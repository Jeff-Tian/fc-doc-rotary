# 0-135;200;205;210;211;231;300;400;405;410;411;420;431;500;600;700;800;900;1000;1015;1030;1031;1100;1101;1120;1121;1200;2000;2001;2020;2021;2100;2101;2120;2121;10000;11000;11001;11020;11021;11100;11101;11120;11121;20000;21000;21001;21020;21021;21200;21201;21220;21221;22000;100000;300000;300001;310000;321000;321321;1000000;1000001;1000023;4000435;11320387;34045671940;1.1;1.2;2,01;2.21;2.22;2.222;2,2222;45,9876543;-5;−17,48

^0 нула
1 едно
2 две
3 три
4 четири
5 пет
6 шест
7 седем
8 осем
9 девет
10 десет
11 единадесет
12 дванадесет
1(\d) $1надесет
(\d)(\d) $(m:$1)десет[ и $2]
(\d)(\d\d) $(e2:\1)[ $(and:$2)]
(\d{1,3})(\d{3}) $(e3:\1)[ $(and:$2)]
(\d{1,3})(\d{6}) $(e6:\1)[ $(and:$2)]
(\d{1,3})(\d{9}) $(e9:\1)[ $(and:$2)]
(\d{1,3})(\d{12}) $(e12:\1)[ $(and:$2)]
(\d{1,3})(\d{15}) $(e15:\1)[ $(and:$2)]
(\d{1,3})(\d{18}) $(e18:\1)[ $(and:$2)]
(\d{1,3})(\d{21}) $(e21:\1)[ $(and:$2)]
(\d{1,3})(\d{24}) $(e24:\1)[ $(and:$2)]

e2:1 сто
e2:([23]) $1ста
e2:(\d) $1стотин
e3:1 хиляда
e3:(\d{1,3}) $(f:$1) хиляди
e6:1 един милион
e6:(\d{1,3}) $(m:$1) милиона
e9:1 един милиард
e9:(\d{1,3}) $(m:$1) милиарда
e12:1 един трилион
e12:(\d{1,3}) $(m:$1) трилиона
e15:1 един квадрилион
e15:(\d{1,3}) $(m:$1) квадрилиона
e18:1 един квинтилион
e18:(\d{1,3}) $(m:$1) квинтилиона
e21:1 един секстилион
e21:(\d{1,3}) $(m:$1) секстилиона
e24:1 един септилион
e24:(\d{1,3}) $(m:$1) септилиона

# prepend "и" ("and") only to a subphrase that does not already contain it

"and:([а-я ]+ и \D+)" \1
"and:(\D+)" и \1

# negative number

[-−](\d+) минус |$1

# decimals

by_digits:(\d)(\d*) $1 $(by_digits:\2)
([-−]?\d+)[.,] цяло
([-−]?\d+)[.,]1 $1 цяло и една десета
([-−]?\d+)[.,](\d) $1 цяло и $(f:|$2) десети
([-−]?\d+)[.,]01 $1 цяло и една стотна
([-−]?\d+)[.,](\d\d) $1 цяло и $(f:|$2) стотни
([-−]?\d+)[.,]001 $1 цяло и една хилядна
([-−]?\d+)[.,](\d{3}) $1 цяло и $(f:|$2) хилядни
([-−]?\d+)[.,]0001 $1 цяло и една десетохилядна
([-−]?\d+)[.,](\d{4}) $1 цяло и $(f:|$2) десетохилядни
([-−]?\d+)[.,]00001 $1 цяло и една стохилядна
([-−]?\d+)[.,](\d{5}) $1 цяло и $(f:|$2) стохилядни
([-−]?\d+)[.,]000001 $1 цяло и една милионна
([-−]?\d+)[.,](\d{6}) $1 цяло и $(f:|$2) милионни
([-−]?\d+)[.,](\d+) $1 цяло и $(by_digits:\2)

# gender conversion for cardinals (default is neuter)

n:(\D+) \1
f:(\D*)едно \1една
f:(\D+) \1
m:(\D*)едно \1един
m:(\D*)две \1два
m:(\D+) \1

# currency

# unit/subunit singular/plural

us(.).:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) $(\1:\6) \2
up(.).:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) $(\1:\6) \3
ss.(.):([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) $(\1:\6) \4
sp.(.):([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) $(\1:\6) \5

BGN:(.+),(.+) $(\2mf:лев,лева,стотинка,стотинки,\1)
CHF:(.+),(.+) $(\2mm:швейцарски франк,швейцарски франка,сантим,сантима,\1)
CNY:(.+),(.+) $(\2mm:юан,юана,фен,фена,\1)
EUR:(.+),(.+) $(\2nm:евро,евро,цент,цента,\1)
GBP:(.+),(.+) $(\2fm:британска лира,британски лири,пенс,пенса,\1)
JPY:(.+),(.+) $(\2fm:йена,йени,сен,сена,\1)
RUB:(.+),(.+) $(\2ff:рубла,рубли,копейка,копейки,\1)
TRY:(.+),(.+) $(\2fm:турска лира,турски лири,куруш,куруша,\1)
USD:(.+),(.+) $(\2mm:щатски долар,щатски долара,цент,цента,\1)

"([A-Z]{3}) ([-−]?1)([.,]00?)?" $(\1:|$2,us)
"([A-Z]{3}) ([-−]?\d+)([.,]00?)?" $(\1:|$2,up)

"(([A-Z]{3}) [-−]?\d+)[.,](01)" $1 и $(\2:|$3,ss)
"(([A-Z]{3}) [-−]?\d+)[.,](\d)" $1 и $(\2:|$(\30),sp)
"(([A-Z]{3}) [-−]?\d+)[.,](\d\d)" $1 и $(\2:|$3,sp)

== neuter ==

([-−]?\d+) $1

== masculine ==

([-−]?\d+) $(m:|$1)

== feminine ==

([-−]?\d+) $(f:|$1)

== ordinal ==

# up to 6 digits, more than enough for outline numbering

0 нулев
1 първи
2 втори
3 трети
4 четвърти
5 пети
6 шести
7 седми
8 осми
9 девети
(1\d|[2-9]0) $1и
(\d)(\d) $(\10)[ и $(ordinal \2)]
100 стотен
([23])00 $1стен
(\d)00 $1стотен
(\d)(\d\d) $(\100) $(and:$(ordinal \2))
1000 хиляден
(1?\d|[1-9]00?)000 $(f:|$1)хиляден
(\d{2,3})000 $(f:|$1) хиляден
1(\d{3}) хиляда[ $(and:$(ordinal \1))]
(\d{1,3})(\d{3}) $(f:|$1) хиляди[ $(and:$(ordinal \2)])

[-−](\d+) минус $(ordinal \1)

== ordinal-masculine ==

([-−]?\d+) $(ordinal \1)

== ordinal-feminine ==

([-−]?\d+) $(ordinal-feminine $(ordinal \1))

(\D+)ен \1на
(\D+)и \1а
(\D+) \1а

== ordinal-neuter ==

([-−]?\d+) $(ordinal-neuter $(ordinal \1))

(\D+)ен \1но
(\D+)и \1о
(\D+) \1о

== ordinal-number ==

(\d+) \1.

== help ==

"" $(1)|, $(2), $(3)\n$(\0 masculine)$(\0 feminine)$(\0 neuter)$(\0 ordinal-masculine)$(\0 ordinal-feminine)$(\0 ordinal-neuter)$(\0 ordinal-number)
(.*) \1: |$(\1 1)|, |$(\1 2)|, |$(\1 3)|\n